# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//brave/vendor/challenge_bypass_ristretto_ffi/config.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}
if (is_android || defined(is_docker)) {
  dep_base = rebase_path("./", "//")
} else {
  dep_base = rebase_path("../..", "//")
}

config("external_config") {
  visibility = [
    ":*",
  ]
  include_dirs = [ "include" ]
}

config("internal_config") {
  visibility = [
    ":*",
    "//brave/test:*",
  ]
  include_dirs = [ "src" ]
}

source_set("bat-native-confirmations") {
  public_configs = [ ":external_config" ]
  configs += [ ":internal_config" ]

  visibility = [
    ":*",
    rebase_path("bat-native-ledger", dep_base) + ":*",
    "//brave/test:*",
  ]

  sources = [
    "src/bat/confirmations/notification_info.cc",
    "src/bat/confirmations/issuer_info.cc",
    "src/bat/confirmations/issuers_info.cc",
    "src/bat/confirmations/wallet_info.cc",
    "src/confirmations.cc",
    "src/refill_tokens.cc",
    "src/request_signed_tokens_request.cc",
    "src/get_signed_tokens_request.cc",
    "src/redeem_token.cc",
    "src/create_confirmation_request.cc",
    "src/fetch_payment_token_request.cc",
    "src/payout_tokens.cc",
    "src/redeem_payment_tokens_request.cc",
    "src/unblinded_tokens.cc",
    "src/ads_serve_helper.cc",
    "src/string_helper.cc",
    "src/security_helper.cc",
    "src/confirmations_impl.cc",
  ]

  public_deps = [
    ":challenge_bypass_libs",
  ]

  deps = [
    "//base",
    "//third_party/boringssl",
    rebase_path("bat-native-ledger:headers", dep_base),
    rebase_path("bat-native-tweetnacl:tweetnacl", dep_base),
    rebase_path("challenge_bypass_ristretto_ffi", dep_base),
  ]
}

if (is_mac) {
  bundle_data("challenge_bypass_libs") {
    sources = [
      challenge_bypass_lib_path,
    ]
    outputs = [
      "{{bundle_contents_dir}}/Libraries/{{source_file_part}}",
    ]
    public_deps = [
      rebase_path("challenge_bypass_ristretto_ffi:challenge_bypass_ristretto", dep_base),
    ]
  }
} else {
  group("challenge_bypass_libs") {}
}
