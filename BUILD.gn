# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}
if (is_android || defined(is_docker)) {
  dep_base = rebase_path("./", "//")
} else {
  dep_base = rebase_path("../..", "//")
}

config("external_config") {
  include_dirs = [ "include" ]
}

config("internal_config") {
  include_dirs = [ "src" ]
}

group("bat-native-confirmations") {
  public_deps = [
    ":confirmations",
  ]

  data_deps = [
    ":bat-native-confirmations-standalone",
  ]
}

static_library("bat-native-confirmations-standalone") {
  # TODO(bridiver) - only export symbols from `include`
  complete_static_lib = true

  output_dir = root_out_dir

  configs -= [ "//build/config/compiler:thin_archive" ]

  defines = [
    "STANDALONE_BUILD",
  ]

  deps = [
    ":confirmations",
  ]
}

source_set("confirmations") {
  public_configs = [ ":external_config" ]
  configs += [ ":internal_config" ]

  output_name = "bat_native_confirmations"

  if (is_mac || is_ios) { 
    libs = [
      "Security.framework"
    ]
  }

  sources = [
    "src/bat/confirmations/notification_info.cc",
    "src/bat/confirmations/issuer_info.cc",
    "src/bat/confirmations/issuers_info.cc",
    "src/bat/confirmations/wallet_info.cc",
    "src/confirmations.cc",
    "src/refill_tokens.cc",
    "src/request_signed_tokens_request.cc",
    "src/get_signed_tokens_request.cc",
    "src/redeem_token.cc",
    "src/create_confirmation_request.cc",
    "src/fetch_payment_token_request.cc",
    "src/payout_tokens.cc",
    "src/redeem_payment_tokens_request.cc",
    "src/unblinded_tokens.cc",
    "src/ads_serve_helper.cc",
    "src/string_helper.cc",
    "src/security_helper.cc",
    "src/confirmations_impl.cc",
  ]

  deps = [
    "//base",
    "//third_party/boringssl",
    rebase_path("bat-native-tweetnacl:tweetnacl", dep_base),
  ]
}
