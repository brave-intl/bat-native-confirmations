# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}
if (is_android || defined(is_docker)) {
  dep_base = rebase_path("./", "//")
} else {
  dep_base = rebase_path("../..", "//")
}

config("external_config") {
  include_dirs = [ "include" ]
}

config("internal_config") {
  include_dirs = [ "src" ]
}

executable("bnc-test") {
  include_dirs = [
  ]

  sources = [
    "src/bat_native_confirmations.cc",
    "src/mock_confirmations_client.cc",
  ]

  deps = [
    ":confirmations",
  ]
}

group("bat-native-confirmations") {
  public_deps = [
    ":confirmations",
  ]

  data_deps = [
    ":bat-native-confirmations-standalone",
  ]
}

static_library("bat-native-confirmations-standalone") {
  complete_static_lib = true

  output_dir = root_out_dir

  configs -= [ "//build/config/compiler:thin_archive" ]

  defines = [
    "STANDALONE_BUILD",
  ]

  deps = [
    ":confirmations",
  ]
}

source_set("confirmations") {
  public_configs = [ ":external_config" ]
  configs += [ ":internal_config" ]

  output_name = "bat_native_confirmations"

  if (is_mac || is_ios ) { 
    libs = [
      "Security.framework"
    ]
  }

  include_dirs = [
  ]

  sources = [
    "src/bat/confirmations/notification_info.cc",
    "src/bat/confirmations/issuer_info.cc",
    "src/bat/confirmations/issuers_info.cc",
    "src/bat/confirmations/wallet_info.cc",
    "src/confirmations.cc",
    "src/confirmations_impl.cc",
  ]

  deps = [
    "//base",
    "//net",
    "//third_party/boringssl",
    "//brave/common",
    "//chrome/common",
    rebase_path("bat-native-tweetnacl:tweetnacl", dep_base),
  ]
}
